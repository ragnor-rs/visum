buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.2'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath "com.fernandocejas.frodo:frodo-plugin:0.8.3"
    }
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'me.tatarka.retrolambda'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "io.reist.sandbox"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'NOTICE'
        exclude "asm-license.txt"
    }

}

def supportVersion = '23.4.0'
def retrofitVersion = '2.0.0-beta4'
def storioVersion = '1.5.0'
def daggerVersion = '2.0.1'

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // support libraries
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support:support-annotations:$supportVersion"

    // IoC
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'

    // Network layer
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    // Database layer
    compile "com.pushtorefresh.storio:sqlite:$storioVersion"
    compile "com.pushtorefresh.storio:sqlite-annotations:$storioVersion"
    apt "com.pushtorefresh.storio:sqlite-annotations-processor:$storioVersion"

    // UI
    compile 'com.jakewharton:butterknife:7.0.1'

    compile project(':library')

    // junit tests

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'org.assertj:assertj-core:2.2.0'
    testCompile "org.mockito:mockito-core:1.10.19"

    testApt 'com.google.dagger:dagger-compiler:2.0.1'

    // android tests

    androidTestApt 'com.google.dagger:dagger-compiler:2.0.1'

    androidTestCompile ('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

}