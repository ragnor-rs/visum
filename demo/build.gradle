buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven {url "https://maven.google.com" }
}

apply plugin: 'com.android.application'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {

        applicationId "io.reist.sandbox"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'NOTICE'
        exclude "asm-license.txt"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

def supportVersion = '27.0.0'
def retrofitVersion = '2.3.0'
def storioVersion = '1.13.0'
def daggerVersion = '2.15'
def butterknifeVersion = '8.6.0'
def supportTestVersion = '0.5'
def espressoVersion = '2.2.2'
def playServicesVersion = '12.0.1'
def roboctricVersion = "3.8"

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:multidex:1.0.3'

    // support libraries
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-annotations:$supportVersion"

    // IoC
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Network layer
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    // Database layer
    implementation "com.pushtorefresh.storio:sqlite:$storioVersion"
    implementation "com.pushtorefresh.storio:sqlite-annotations:$storioVersion"
    annotationProcessor "com.pushtorefresh.storio:sqlite-annotations-processor:$storioVersion"

    // UI
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor ("com.jakewharton:butterknife-compiler:$butterknifeVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation 'com.github.ragnor-rs:dali:57660bf09f77c5fb3b3fcdfec7409618b9f1708f'
    implementation 'com.github.ragnor-rs:el-adapter:63428bb46c6b06b9cd10e204014c591c3f3cb7d9'

    // map
    implementation 'com.google.android.gms:play-services-maps:' + playServicesVersion
    implementation 'com.google.android.gms:play-services-location:' + playServicesVersion

    // rx
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    
    implementation project(':library')

    // junit tests

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.8.47"
    testImplementation 'org.assertj:assertj-core:3.8.0'

    testImplementation "org.robolectric:robolectric:" + roboctricVersion
    testImplementation "org.robolectric:shadows-multidex:" + roboctricVersion

    testAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // android tests

    androidTestImplementation "com.android.support:support-annotations:$supportVersion"

    androidTestImplementation ("com.android.support.test:runner:$supportTestVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation "com.android.support.test:rules:$supportTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"

    androidTestImplementation ("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
    }

}
